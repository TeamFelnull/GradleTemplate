println 'Used ikisugi gradle template...'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "${project.mod_name}-${project.mc_version}-Fabric"
version = project.mod_version
group = project.mod_group

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.mc_version}"
    mappings minecraft.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fcloader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fc_version}"

    if (project.cloth_config_version != '')
        modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"

    if (project.modmenu_version != '')
        modApi "com.terraformersmc:modmenu:${project.modmenu_version}"

    if (project.rei_version != '') {
        modApi  "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
        modRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.mod_name}" }
    }

}

java {
    withSourcesJar()
}

